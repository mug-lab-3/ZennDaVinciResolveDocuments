@echo off
setlocal enabledelayedexpansion
chcp 65001 > nul

:: 1. è¨­ç½®å ´æ‰€ã®ãƒã‚§ãƒƒã‚¯
set "ffmpegPath=%~dp0bin\ffmpeg.exe"

if not exist "!ffmpegPath!" (
    echo âŒ ã‚¨ãƒ©ãƒ¼: è¨­ç½®å ´æ‰€ãŒé–“é•ã£ã¦ã„ã‚‹ã¿ãŸã„...ğŸ˜±
    echo ã€Œbinã€ãƒ•ã‚©ãƒ«ãƒ€ã®ä¸­ã«ã€Œffmpeg.exeã€ãŒè¦‹ã¤ã‹ã‚Šã¾ã›ã‚“ã§ã—ãŸ
    echo.
    echo ç¾åœ¨ã®ç¢ºèªå ´æ‰€: !ffmpegPath!
    echo æ­£ã—ã„å ´æ‰€ã«ãƒãƒƒãƒãƒ•ã‚¡ã‚¤ãƒ«ã‚’ç½®ã„ã¦ã‹ã‚‰ã€ã‚‚ã†ä¸€åº¦è©¦ã—ã¦ãã ã•ã„
    pause
    exit /b
)

:: 2. ãƒ‰ãƒ­ãƒƒãƒ—ã•ã‚ŒãŸãƒ•ã‚¡ã‚¤ãƒ«ã®ãƒã‚§ãƒƒã‚¯
if "%~1" == "" (
    echo âš ï¸ æ–‡å­—èµ·ã“ã—ã—ãŸã„ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ãƒ‰ãƒ­ãƒƒãƒ—ã—ã¦èµ·å‹•ã—ã¦ä¸‹ã•ã„ âš ï¸
    pause
    exit /b
)
set "inputFile=%~1"

:: 3. srtãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã®ä½œæˆ
set "srtDir=%~dp0srt"
if not exist "!srtDir!" (
    mkdir "!srtDir!"
)

:: 4. æ—¥æ™‚å–å¾—
for /f "usebackq" %%i in (`powershell -command "Get-Date -Format 'yyyyMMddHHmmss'"`) do (
    set "timestamp=%%i"
)
set "outputFile=!timestamp!.srt"

:: 5. modelsãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã®å­˜åœ¨ãƒã‚§ãƒƒã‚¯
set "targetDir=%~dp0models"
if not exist "!targetDir!" (
    echo ã‚¨ãƒ©ãƒ¼: ã€Œmodelsã€ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªãŒãªã„ã‚ˆğŸ˜‘
    echo !targetDir! ã‚’ä½œæˆã—ã¦ã‹ã‚‰ã‚‚ã†ä¸€åº¦è©¦ã—ã¦ã­
    pause
    exit /b
)

:: 6. ãƒ¢ãƒ‡ãƒ«é¸æŠ
:RETRY
cls
echo ğŸ¤– modelé¸æŠ
echo --------------------------------------------------
echo å ´æ‰€: !targetDir!
echo.

set count=0
for /f "delims=" %%f in ('dir /b /a-d "!targetDir!\*.bin" 2^>nul') do (
    set /a count+=1
    set "file!count!=%%f"
    echo [!count!] %%f
)

if %count%==0 (
    echo ãƒ¢ãƒ‡ãƒ«ãŒå…¥ã£ã¦ãªã„ã‚ˆğŸ’¦ğŸ’¦ å…ˆã«ãƒ¢ãƒ‡ãƒ«ã‚’ãƒ€ã‚¦ãƒ³ãƒ­ãƒ¼ãƒ‰ã—ã¦ã­
    pause
    exit /b
)

echo.
set /p choice="ä½¿ç”¨ã™ã‚‹ãƒ¢ãƒ‡ãƒ«ã‚’é¸ã‚“ã§ç•ªå·(1-%count%)ã‚’å…¥åŠ›ã—ã¦ãã ã•ã„ : "

if not defined file%choice% (
    echo '%choice%'ã¯é¸æŠè‚¢ã«ãªã„ã‚ˆğŸ˜‘ ã‚‚ã†ä¸€åº¦å…¥åŠ›ã—ã¦ãã ã•ã„ğŸ™
    pause
    goto RETRY
)

set "modelName=!file%choice%!"

:: 7. æœ€çµ‚ç¢ºèª
cls
echo ğŸš€ æº–å‚™ã§ãã¾ã—ãŸğŸ«¡
echo --------------------------------------------------
echo ğŸ“¹ å¯¾è±¡ãƒ•ã‚¡ã‚¤ãƒ«    : !inputFile!
echo ğŸ¤– ãƒ¢ãƒ‡ãƒ«          : models/!modelName!
echo ğŸ“ æ–‡å­—èµ·ã“ã—çµæœ  : srt/!outputFile!
echo --------------------------------------------------
echo.
echo ğŸŒ¸ Enterã‚­ãƒ¼ã‚’æŠ¼ã™ã¨å‡¦ç†ã‚’é–‹å§‹ã—ã¾ã™ï¼
pause > nul

echo.
echo ğŸƒ å‡¦ç†ã‚’å®Ÿè¡Œä¸­... ã—ã°ã‚‰ããŠå¾…ã¡ãã ã•ã„
echo --------------------------------------------------

pushd "%~dp0"
"!ffmpegPath!" -itsoffset 01:00:00 -i "!inputFile!" -vn -af "whisper=model=models/!modelName!:queue=20:destination=srt/!outputFile!:format=srt" -f null -
popd

echo --------------------------------------------------
if %errorlevel% equ 0 (
    echo ğŸ‰ æ­£å¸¸ã«å®Œäº†ã—ã¾ã—ãŸï¼
    echo ğŸ“ srt/!outputFile!
) else (
    echo âŒ ä½•ã‹ã‚¨ãƒ©ãƒ¼ãŒç™ºç”Ÿã—ãŸã¿ãŸã„...ğŸ˜±
)
pause